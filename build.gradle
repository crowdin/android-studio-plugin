plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.17.0'
    id 'jacoco'
}

group 'com.crowdin.crowdin-idea'
version '1.6.3'

sourceCompatibility = '17'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {

    implementation 'net.lingala.zip4j:zip4j:2.11.3'
    implementation 'com.github.crowdin:crowdin-api-client-java:1.14.0'
    implementation 'commons-io:commons-io:2.15.1'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:2.1.0'
}

test {
    useJUnitPlatform()
}

intellij {
    version = '2022.3.3'
    plugins = ['Git4Idea']
    updateSinceUntilBuild = false
}
patchPluginXml {
    changeNotes = """
      - Bugfixes"""
}

wrapper {
    gradleVersion = '8.5'
}

jacoco {
    toolVersion = '0.8.10'
    reportsDirectory = layout.buildDirectory.dir('reports')
}

//https://plugins.jetbrains.com/docs/intellij/tools-gradle-intellij-plugin-faq.html#jacoco-reports-0-coverage
test {
    jacoco {
        includeNoLocationClasses = true
        excludes = ["jdk.internal.*"]
    }
}

jacocoTestReport {
    classDirectories.setFrom(instrumentCode)

    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = layout.buildDirectory.file('coverage.xml')
    }

    getExecutionData().setFrom(layout.buildDirectory.file('jacoco/test.exec'))
}

jacocoTestCoverageVerification {
  classDirectories.setFrom(instrumentCode)
}
